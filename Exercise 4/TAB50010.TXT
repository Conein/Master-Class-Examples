OBJECT Table 50010 Example Person
{
  OBJECT-PROPERTIES
  {
    Date=26-05-15;
    Time=15:34:58;
    Modified=Yes;
    Version List=Exercise 2;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 ExampleSetup.GET;
                 ExampleSetup.TESTFIELD("Example Person Nos.");
                 NoSeriesMgt.InitSeries(ExampleSetup."Example Person Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    CaptionML=ENU=Example Person;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ExampleSetup.GET;
                                                                  NoSeriesMgt.TestManual(ExampleSetup."Example Person Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 2   ;   ;Name                ;Text50        ;OnValidate=BEGIN
                                                                IF ("Search Name" = UPPERCASE(xRec.Name)) OR ("Search Name" = '') THEN
                                                                  "Search Name" := Name;
                                                              END;

                                                   CaptionML=ENU=Name }
    { 3   ;   ;Search Name         ;Code50        ;CaptionML=ENU=Search Name }
    { 4   ;   ;Name 2              ;Text50        ;CaptionML=ENU=Name 2 }
    { 5   ;   ;Address             ;Text50        ;CaptionML=ENU=Address }
    { 6   ;   ;Address 2           ;Text50        ;CaptionML=ENU=Address 2 }
    { 7   ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=City }
    { 8   ;   ;Contact             ;Text50        ;CaptionML=ENU=Contact }
    { 9   ;   ;Phone No.           ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Phone No. }
    { 35  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=ENU=Country/Region Code }
    { 39  ;   ;Blocked             ;Option        ;CaptionML=ENU=Blocked;
                                                   OptionCaptionML=ENU=" ,Ship,Invoice,All";
                                                   OptionString=[ ,Ship,Invoice,All] }
    { 91  ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Post Code }
    { 92  ;   ;County              ;Text30        ;CaptionML=ENU=County }
    { 107 ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ExampleSetup@1001 : Record 50000;
      PostCode@1002 : Record 225;
      NoSeriesMgt@1000 : Codeunit 396;

    PROCEDURE AssistEdit@2(OldExamplePerson@1002 : Record 50010) : Boolean;
    VAR
      ExamplePerson@1001 : Record 50010;
    BEGIN
      WITH ExamplePerson DO BEGIN
        ExamplePerson := Rec;
        ExampleSetup.GET;
        ExampleSetup.TESTFIELD("Example Person Nos.");
        IF NoSeriesMgt.SelectSeries(ExampleSetup."Example Person Nos.",OldExamplePerson."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := ExamplePerson;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE FormatAddress@1(VAR AddrArray@1000 : ARRAY [8] OF Text[80]);
    VAR
      FormatAddress@1001 : Codeunit 365;
    BEGIN
      WITH FormatAddress DO
        FormatAddr(AddrArray,Name,"Name 2",Contact,Address,"Address 2",City,"Post Code",County,"Country/Region Code")
    END;

    PROCEDURE FormatAddressArguments@3(VAR AddrArray@1001 : ARRAY [8] OF Text[80]);
    VAR
      AddressAgruments@1000 : Record 50099;
    BEGIN
      WITH AddressAgruments DO BEGIN
        TRANSFERFIELDS(Rec);
        FormAddress(AddrArray);
      END;
    END;

    BEGIN
    END.
  }
}

