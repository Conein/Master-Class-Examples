OBJECT Codeunit 84040 B & B. Res. Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=15-06-15;
    Time=13:28:51;
    Modified=Yes;
    Version List=B&B1.00;
  }
  PROPERTIES
  {
    TableNo=84043;
    OnRun=BEGIN
            GLSetup.GET;
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be a closing date;ESM=no puede ser una fecha £ltima;FRC=ne peut ˆtre une date de fermeture;ENC=cannot be a closing date';
      Text001@1001 : TextConst 'ENU=is not within your range of allowed posting dates;ESM=no est  dentro del periodo de fechas de registro permitidas;FRC=n''est pas dans l''intervalle de dates de report permis;ENC=is not within your range of allowed posting dates';
      Text002@1002 : TextConst 'ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5;ESM=La combin. de dimensiones utilizada en %1 %2, %3, %4 est  bloq. %5;FRC=La combinaison de dimensions utilis‚e dans %1 %2, %3, %4 est bloqu‚e. %5;ENC=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5';
      Text003@1003 : TextConst 'ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5;ESM=La dimensi¢n util. en %1 %2, %3, %4 ha causado error. %5;FRC=Une erreur a ‚t‚ caus‚e par une dimension utilis‚e dans %1 %2, %3, %4. %5;ENC=A dimension used in %1 %2, %3, %4 has caused an error. %5';
      GLSetup@1004 : Record 98;
      UserSetup@1005 : Record 91;
      DimMgt@1007 : Codeunit 408;
      AllowPostingFrom@1008 : Date;
      AllowPostingTo@1009 : Date;

    PROCEDURE RunCheck@1(VAR ResJnlLine@1000 : Record 84043);
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      WITH ResJnlLine DO BEGIN
        IF EmptyLine THEN
          EXIT;

        TESTFIELD("Check-In Date");
        TESTFIELD("Check-Out Date");
        TESTFIELD(Name);

        IF "Check-In Date" <> NORMALDATE("Check-In Date") THEN
          FIELDERROR("Check-In Date",Text000);

        IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
          IF USERID <> '' THEN
            IF UserSetup.GET(USERID) THEN BEGIN
              AllowPostingFrom := UserSetup."Allow Posting From";
              AllowPostingTo := UserSetup."Allow Posting To";
            END;
          IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
            GLSetup.GET;
            AllowPostingFrom := GLSetup."Allow Posting From";
            AllowPostingTo := GLSetup."Allow Posting To";
          END;
          IF AllowPostingTo = 0D THEN
            AllowPostingTo := 31129999D;
        END;
        IF ("Check-In Date" < AllowPostingFrom) OR ("Check-In Date" > AllowPostingTo) THEN
          FIELDERROR("Check-In Date",Text001);

        IF "Check-Out Date" <> 0D THEN
          IF "Check-Out Date" <> NORMALDATE("Check-Out Date") THEN
            FIELDERROR("Check-Out Date",Text000);

        IF NOT DimMgt.CheckDimIDComb("Dimension Set ID") THEN
          ERROR(
            Text002,
            TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
            DimMgt.GetDimCombErr);

        TableID[1] := DATABASE::Table84020;
        No[1] := "Guest No.";
        IF NOT DimMgt.CheckDimValuePosting(TableID,No,"Dimension Set ID") THEN
          IF "Line No." <> 0 THEN
            ERROR(
              Text003,
              TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
              DimMgt.GetDimValuePostingErr)
          ELSE
            ERROR(DimMgt.GetDimValuePostingErr);
      END;
    END;

    BEGIN
    END.
  }
}

