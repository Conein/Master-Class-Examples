OBJECT Table 91 User Setup
{
  OBJECT-PROPERTIES
  {
    Date=26-01-17;
    Time=14:36:13;
    Modified=Yes;
    Version List=NAVW110.00,Exercise 1;
  }
  PROPERTIES
  {
    OnDelete=VAR
               NotificationSetup@1000 : Record 1512;
             BEGIN
               NotificationSetup.SETRANGE("User ID","User ID");
               NotificationSetup.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=User Setup;
               NLD=Gebruikersinstellingen];
    LookupPageID=Page119;
    DrillDownPageID=Page119;
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=VAR
                                                                UserMgt@1000 : Codeunit 418;
                                                              BEGIN
                                                                UserMgt.ValidateUserID("User ID");
                                                              END;

                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              NLD=Gebruikers-id];
                                                   NotBlank=Yes }
    { 2   ;   ;Allow Posting From  ;Date          ;CaptionML=[ENU=Allow Posting From;
                                                              NLD=Boeken toegest. vanaf] }
    { 3   ;   ;Allow Posting To    ;Date          ;CaptionML=[ENU=Allow Posting To;
                                                              NLD=Boeken toegest. tot] }
    { 4   ;   ;Register Time       ;Boolean       ;CaptionML=[ENU=Register Time;
                                                              NLD=Tijd registreren] }
    { 10  ;   ;Salespers./Purch. Code;Code10      ;TableRelation=Salesperson/Purchaser.Code;
                                                   OnValidate=VAR
                                                                UserSetup@1000 : Record 91;
                                                              BEGIN
                                                                IF "Salespers./Purch. Code" <> '' THEN BEGIN
                                                                  UserSetup.SETCURRENTKEY("Salespers./Purch. Code");
                                                                  UserSetup.SETRANGE("Salespers./Purch. Code","Salespers./Purch. Code");
                                                                  IF UserSetup.FINDFIRST THEN
                                                                    ERROR(Text001,"Salespers./Purch. Code",UserSetup."User ID");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Salespers./Purch. Code;
                                                              NLD=Verkoper/Inkoper] }
    { 11  ;   ;Approver ID         ;Code50        ;TableRelation="User Setup"."User ID";
                                                   OnValidate=BEGIN
                                                                IF "Approver ID" = "User ID" THEN
                                                                  FIELDERROR("Approver ID");
                                                              END;

                                                   OnLookup=VAR
                                                              UserSetup@1000 : Record 91;
                                                            BEGIN
                                                              UserSetup.SETFILTER("User ID",'<>%1',"User ID");
                                                              IF PAGE.RUNMODAL(PAGE::"Approval User Setup",UserSetup) = ACTION::LookupOK THEN
                                                                VALIDATE("Approver ID",UserSetup."User ID");
                                                            END;

                                                   CaptionML=[ENU=Approver ID;
                                                              NLD=Fiatteur-id] }
    { 12  ;   ;Sales Amount Approval Limit;Integer;OnValidate=BEGIN
                                                                IF "Unlimited Sales Approval" AND ("Sales Amount Approval Limit" <> 0) THEN
                                                                  ERROR(Text003,FIELDCAPTION("Sales Amount Approval Limit"),FIELDCAPTION("Unlimited Sales Approval"));
                                                                IF "Sales Amount Approval Limit" < 0 THEN
                                                                  ERROR(Text005);
                                                              END;

                                                   CaptionML=[ENU=Sales Amount Approval Limit;
                                                              NLD=Limiet voor goedkeuring verkoopbedrag];
                                                   BlankZero=Yes }
    { 13  ;   ;Purchase Amount Approval Limit;Integer;
                                                   OnValidate=BEGIN
                                                                IF "Unlimited Purchase Approval" AND ("Purchase Amount Approval Limit" <> 0) THEN
                                                                  ERROR(Text003,FIELDCAPTION("Purchase Amount Approval Limit"),FIELDCAPTION("Unlimited Purchase Approval"));
                                                                IF "Purchase Amount Approval Limit" < 0 THEN
                                                                  ERROR(Text005);
                                                              END;

                                                   CaptionML=[ENU=Purchase Amount Approval Limit;
                                                              NLD=Limiet voor goedkeuring inkoopbedrag];
                                                   BlankZero=Yes }
    { 14  ;   ;Unlimited Sales Approval;Boolean   ;OnValidate=BEGIN
                                                                IF "Unlimited Sales Approval" THEN
                                                                  "Sales Amount Approval Limit" := 0;
                                                              END;

                                                   CaptionML=[ENU=Unlimited Sales Approval;
                                                              NLD=Onbeperkte goedkeuring van verkopen] }
    { 15  ;   ;Unlimited Purchase Approval;Boolean;OnValidate=BEGIN
                                                                IF "Unlimited Purchase Approval" THEN
                                                                  "Purchase Amount Approval Limit" := 0;
                                                              END;

                                                   CaptionML=[ENU=Unlimited Purchase Approval;
                                                              NLD=Onbeperkte goedkeuring van inkopen] }
    { 16  ;   ;Substitute          ;Code50        ;TableRelation="User Setup"."User ID";
                                                   OnValidate=BEGIN
                                                                IF Substitute = "User ID" THEN
                                                                  FIELDERROR(Substitute);
                                                              END;

                                                   OnLookup=VAR
                                                              UserSetup@1000 : Record 91;
                                                            BEGIN
                                                              UserSetup.SETFILTER("User ID",'<>%1',"User ID");
                                                              IF PAGE.RUNMODAL(PAGE::"Approval User Setup",UserSetup) = ACTION::LookupOK THEN
                                                                VALIDATE(Substitute,UserSetup."User ID");
                                                            END;

                                                   CaptionML=[ENU=Substitute;
                                                              NLD=Vervanger] }
    { 17  ;   ;E-Mail              ;Text100       ;ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=E-Mail;
                                                              NLD=E-mail] }
    { 19  ;   ;Request Amount Approval Limit;Integer;
                                                   OnValidate=BEGIN
                                                                IF "Unlimited Request Approval" AND ("Request Amount Approval Limit" <> 0) THEN
                                                                  ERROR(Text003,FIELDCAPTION("Request Amount Approval Limit"),FIELDCAPTION("Unlimited Request Approval"));
                                                                IF "Request Amount Approval Limit" < 0 THEN
                                                                  ERROR(Text005);
                                                              END;

                                                   CaptionML=[ENU=Request Amount Approval Limit;
                                                              NLD=Limiet voor goedkeuring aanvraagbedrag];
                                                   BlankZero=Yes }
    { 20  ;   ;Unlimited Request Approval;Boolean ;OnValidate=BEGIN
                                                                IF "Unlimited Request Approval" THEN
                                                                  "Request Amount Approval Limit" := 0;
                                                              END;

                                                   CaptionML=[ENU=Unlimited Request Approval;
                                                              NLD=Onbeperkt aanvragen van goedkeuring] }
    { 21  ;   ;Approval Administrator;Boolean     ;OnValidate=VAR
                                                                UserSetup@1000 : Record 91;
                                                              BEGIN
                                                                IF "Approval Administrator" THEN BEGIN
                                                                  UserSetup.SETRANGE("Approval Administrator",TRUE);
                                                                  IF NOT UserSetup.ISEMPTY THEN
                                                                    FIELDERROR("Approval Administrator");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Approval Administrator;
                                                              NLD=Beheerder goedkeuringssysteem] }
    { 31  ;   ;License Type        ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(User."License Type" WHERE (User Name=FIELD(User ID)));
                                                   CaptionML=[ENU=License Type;
                                                              NLD=Licentietype];
                                                   OptionCaptionML=[ENU=Full User,Limited User,Device Only User,Windows Group,External User;
                                                                    NLD=Volwaardige gebruiker,Beperkte gebruiker,Alleen apparaat,Windows-groep,Externe gebruiker];
                                                   OptionString=Full User,Limited User,Device Only User,Windows Group,External User }
    { 950 ;   ;Time Sheet Admin.   ;Boolean       ;CaptionML=[ENU=Time Sheet Admin.;
                                                              NLD=Urenstaat-admin.] }
    { 5600;   ;Allow FA Posting From;Date         ;CaptionML=[ENU=Allow FA Posting From;
                                                              NLD=VA-boeken toegest. vanaf] }
    { 5601;   ;Allow FA Posting To ;Date          ;CaptionML=[ENU=Allow FA Posting To;
                                                              NLD=VA-boeken toegest. tot] }
    { 5700;   ;Sales Resp. Ctr. Filter;Code10     ;TableRelation="Responsibility Center".Code;
                                                   CaptionML=[ENU=Sales Resp. Ctr. Filter;
                                                              NLD=Verkoopdivisiefilter] }
    { 5701;   ;Purchase Resp. Ctr. Filter;Code10  ;TableRelation="Responsibility Center";
                                                   CaptionML=[ENU=Purchase Resp. Ctr. Filter;
                                                              NLD=Inkoopdivisiefilter] }
    { 5900;   ;Service Resp. Ctr. Filter;Code10   ;TableRelation="Responsibility Center";
                                                   CaptionML=[ENU=Service Resp. Ctr. Filter;
                                                              NLD=Servicedivisiefilter] }
    { 50000;  ;Example Enabled     ;Boolean        }
  }
  KEYS
  {
    {    ;User ID                                 ;Clustered=Yes }
    {    ;Salespers./Purch. Code                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=The %1 Salesperson/Purchaser code is already assigned to another User ID %2.;NLD=Verkoper-/inkopercode %1 is al toegewezen aan gebruikers-id %2.';
      Text003@1002 : TextConst 'ENU="You cannot have both a %1 and %2. ";NLD="U kunt niet zowel %1 als %2 hebben. "';
      Text005@1004 : TextConst 'ENU=You cannot have approval limits less than zero.;NLD=Goedkeuringslimieten mogen niet kleiner dan nul zijn.';

    PROCEDURE CreateApprovalUserSetup@3(User@1000 : Record 2000000120);
    VAR
      UserSetup@1001 : Record 91;
      ApprovalUserSetup@1002 : Record 91;
    BEGIN
      ApprovalUserSetup.INIT;
      ApprovalUserSetup.VALIDATE("User ID",User."User Name");
      ApprovalUserSetup.VALIDATE("Sales Amount Approval Limit",GetDefaultSalesAmountApprovalLimit);
      ApprovalUserSetup.VALIDATE("Purchase Amount Approval Limit",GetDefaultPurchaseAmountApprovalLimit);
      ApprovalUserSetup.VALIDATE("E-Mail",User."Contact Email");
      UserSetup.SETRANGE("Sales Amount Approval Limit",UserSetup.GetDefaultSalesAmountApprovalLimit);
      IF UserSetup.FINDFIRST THEN
        ApprovalUserSetup.VALIDATE("Approver ID",UserSetup."Approver ID");
      IF ApprovalUserSetup.INSERT THEN;
    END;

    PROCEDURE GetDefaultSalesAmountApprovalLimit@1() : Integer;
    VAR
      UserSetup@1001 : Record 91;
      DefaultApprovalLimit@1000 : Integer;
      LimitedApprovers@1002 : Integer;
    BEGIN
      UserSetup.SETRANGE("Unlimited Sales Approval",FALSE);

      IF UserSetup.FINDFIRST THEN BEGIN
        DefaultApprovalLimit := UserSetup."Sales Amount Approval Limit";
        LimitedApprovers := UserSetup.COUNT;
        UserSetup.SETRANGE("Sales Amount Approval Limit",DefaultApprovalLimit);
        IF LimitedApprovers = UserSetup.COUNT THEN
          EXIT(DefaultApprovalLimit);
      END;

      // Return 0 if no user setup exists or no default value is found
      EXIT(0);
    END;

    PROCEDURE GetDefaultPurchaseAmountApprovalLimit@2() : Integer;
    VAR
      UserSetup@1002 : Record 91;
      DefaultApprovalLimit@1001 : Integer;
      LimitedApprovers@1000 : Integer;
    BEGIN
      UserSetup.SETRANGE("Unlimited Purchase Approval",FALSE);

      IF UserSetup.FINDFIRST THEN BEGIN
        DefaultApprovalLimit := UserSetup."Purchase Amount Approval Limit";
        LimitedApprovers := UserSetup.COUNT;
        UserSetup.SETRANGE("Purchase Amount Approval Limit",DefaultApprovalLimit);
        IF LimitedApprovers = UserSetup.COUNT THEN
          EXIT(DefaultApprovalLimit);
      END;

      // Return 0 if no user setup exists or no default value is found
      EXIT(0);
    END;

    PROCEDURE HideExternalUsers@5();
    VAR
      PermissionManager@1001 : Codeunit 9002;
      OriginalFilterGroup@1000 : Integer;
    BEGIN
      IF NOT PermissionManager.SoftwareAsAService THEN
        EXIT;

      OriginalFilterGroup := FILTERGROUP;
      FILTERGROUP := 2;
      CALCFIELDS("License Type");
      SETFILTER("License Type",'<>%1',"License Type"::"External User");
      FILTERGROUP := OriginalFilterGroup;
    END;

    BEGIN
    END.
  }
}

OBJECT Table 50000 Example Setup
{
  OBJECT-PROPERTIES
  {
    Date=09-03-17;
    Time=14:10:48;
    Modified=Yes;
    Version List=Exercise 1;
  }
  PROPERTIES
  {
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Example Enabled     ;Boolean       ;CaptionML=ENU=Example Enabled }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE InitSetupRecord@50000();
    BEGIN
      IF NOT GET THEN BEGIN
        INIT;
        INSERT;
      END;
    END;

    BEGIN
    END.
  }
}

OBJECT Codeunit 50011 Example Enabled iFace
{
  OBJECT-PROPERTIES
  {
    Date=04-11-15;
    Time=19:51:56;
    Modified=Yes;
    Version List=Exercise 1;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE CheckEnabled@2() : Boolean;
    VAR
      UserSetup@1000 : Record 91;
      ExampleSetup@1001 : Record 50000;
    BEGIN
      IF USERID <> '' THEN
        IF UserSetup.GET(USERID) THEN
          IF UserSetup."Example Enabled" THEN
            EXIT(TRUE);

      WITH ExampleSetup DO BEGIN
        GET;
        EXIT("Example Enabled");
      END;
    END;

    BEGIN
    END.
  }
}

OBJECT Codeunit 50098 Example Testrunner
{
  OBJECT-PROPERTIES
  {
    Date=04-11-15;
    Time=19:54:13;
    Modified=Yes;
    Version List=Exercise 1;
  }
  PROPERTIES
  {
    Subtype=TestRunner;
    TestIsolation=Codeunit;
    OnRun=VAR
            ExampleTest@50000 : Codeunit 50099;
          BEGIN
            ExampleTest.RUN;
          END;

  }
  CODE
  {

    BEGIN
    END.
  }
}

OBJECT Codeunit 50099 Example Test
{
  OBJECT-PROPERTIES
  {
    Date=26-11-15;
    Time=13:51:26;
    Modified=Yes;
    Version List=Exercise 1;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [Test]
    PROCEDURE TestInitSetup@50000();
    VAR
      ExampleSetup@50000 : Record 50000;
    BEGIN
      WITH ExampleSetup DO BEGIN
        InitSetupRecord;
        GET;
      END;
    END;

    BEGIN
    END.
  }
}

OBJECT Codeunit 51002 Example Company-Initialize
{
  OBJECT-PROPERTIES
  {
    Date=21-03-17;
    Time=12:05:46;
    Modified=Yes;
    Version List=Exercise 1;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Codeunit,2,OnCompanyInitialize)]
    LOCAL PROCEDURE InitSetupTables@1000000001();
    VAR
      ExampleSetup@1000000000 : Record 50000;
    BEGIN
      ExampleSetup.InitSetupRecord;
    END;

    BEGIN
    END.
  }
}

OBJECT Page 119 User Setup
{
  OBJECT-PROPERTIES
  {
    Date=26-01-17;
    Time=14:36:28;
    Modified=Yes;
    Version List=NAVW110.00,Exercise 1;
  }
  PROPERTIES
  {
    CaptionML=[ENU=User Setup;
               NLD=Gebruikersinstellingen];
    SourceTable=Table91;
    PageType=List;
    OnOpenPage=BEGIN
                 HideExternalUsers;
               END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies a user ID.;
                           NLD=Hiermee wordt een gebruikers-id opgegeven.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="User ID" }

    { 1000000000;2;Field  ;
                SourceExpr="Example Enabled" }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the earliest date on which the user is allowed to post to the company.;
                           NLD=Hiermee wordt de vroegste datum opgegeven waarop de gebruiker naar het bedrijf mag boeken.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Allow Posting From" }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the last date on which the user is allowed to post to the company.;
                           NLD=Hiermee wordt de laatste datum opgegeven waarop de gebruiker naar het bedrijf mag boeken.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Allow Posting To" }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies whether to register the user's time usage defined as the time spent from when the user logs in to when the user logs out.;
                           NLD=Hiermee wordt opgegeven of het tijdsgebruik van de gebruiker moeten worden geregistreerd, gedefinieerd als tijd die is besteed vanaf het aanmelden door de gebruiker tot het afmelden door de gebruiker.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Register Time" }

    { 15  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the responsibility center to which you want to assign the user.;
                           NLD=Hiermee wordt de code opgegeven voor de divisie waaraan u de gebruiker wilt toewijzen.];
                SourceExpr="Sales Resp. Ctr. Filter" }

    { 17  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the responsibility center to which you want to assign the user.;
                           NLD=Hiermee wordt de code opgegeven voor de divisie waaraan u de gebruiker wilt toewijzen.];
                SourceExpr="Purchase Resp. Ctr. Filter" }

    { 21  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the responsibility center you want to assign to the user. The user will only be able to see service documents for the responsibility center specified in the field. This responsibility center will also be the default responsibility center when the user creates new service documents.;
                           NLD=Hiermee wordt de code opgegeven voor de divisie waaraan u de gebruiker wilt toewijzen. De gebruiker kan alleen de servicedocumenten bekijken voor de divisie die in het veld is opgegeven. Deze divisie is ook de standaarddivisie wanneer de gebruiker nieuwe servicedocumenten maakt.];
                SourceExpr="Service Resp. Ctr. Filter" }

    { 3   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies if a user is a time sheet administrator. A time sheet administrator can access any time sheet and then edit, change, or delete it.;
                           NLD=Hiermee wordt opgegeven of een gebruiker een beheerder van urenstaten is. Een beheerder van urenstaten heeft toegang tot elke urenstaat en kan deze bewerken, wijzigen of verwijderen.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Time Sheet Admin." }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {

    BEGIN
    END.
  }
}

OBJECT Page 50000 Example Setup
{
  OBJECT-PROPERTIES
  {
    Date=26-11-15;
    Time=14:04:04;
    Modified=Yes;
    Version List=Exercise 1;
  }
  PROPERTIES
  {
    CaptionML=ENU=Example Setup;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table50000;
    PageType=Card;
    OnOpenPage=BEGIN
                 InitSetupRecord;
               END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=ENU=General }

    { 2   ;2   ;Field     ;
                SourceExpr="Example Enabled" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {

    BEGIN
    END.
  }
}

