OBJECT Codeunit 50010 ExJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=20-03-15;
    Time=18:53:49;
    Modified=Yes;
    Version List=Exercise 3;
  }
  PROPERTIES
  {
    Permissions=TableData 206=imd,
                TableData 236=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=EXAMPLES';
      Text001@1001 : TextConst 'ENU=Example Journals';
      Text002@1002 : TextConst 'ENU=RECURRING';
      Text003@1003 : TextConst 'ENU=Recurring Example Journal';
      Text004@1004 : TextConst 'ENU=DEFAULT';
      Text005@1005 : TextConst 'ENU=Default Journal';
      OldExNo@1006 : Code[20];
      OpenFromBatch@1007 : Boolean;

    PROCEDURE TemplateSelection@1(PageID@1004 : Integer;RecurringJnl@1000 : Boolean;VAR ExJnlLine@1003 : Record 50013;VAR JnlSelected@1002 : Boolean);
    VAR
      ExJnlTemplate@1001 : Record 50012;
    BEGIN
      JnlSelected := TRUE;

      ExJnlTemplate.RESET;
      ExJnlTemplate.SETRANGE("Page ID",PageID);
      ExJnlTemplate.SETRANGE(Recurring,RecurringJnl);

      CASE ExJnlTemplate.COUNT OF
        0:
          BEGIN
            ExJnlTemplate.INIT;
            ExJnlTemplate.Recurring := RecurringJnl;
            IF NOT RecurringJnl THEN BEGIN
              ExJnlTemplate.Name := Text000;
              ExJnlTemplate.Description := Text001;
            END ELSE BEGIN
              ExJnlTemplate.Name := Text002;
              ExJnlTemplate.Description := Text003;
            END;
            ExJnlTemplate.VALIDATE("Page ID");
            ExJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          ExJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,ExJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        ExJnlLine.FILTERGROUP := 2;
        ExJnlLine.SETRANGE("Journal Template Name",ExJnlTemplate.Name);
        ExJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          ExJnlLine."Journal Template Name" := '';
          PAGE.RUN(ExJnlTemplate."Page ID",ExJnlLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@10(VAR ExJnlBatch@1000 : Record 50014);
    VAR
      ExJnlLine@1002 : Record 50013;
      ExJnlTemplate@1003 : Record 50012;
    BEGIN
      OpenFromBatch := TRUE;
      ExJnlTemplate.GET(ExJnlBatch."Journal Template Name");
      ExJnlTemplate.TESTFIELD("Page ID");
      ExJnlBatch.TESTFIELD(Name);

      ExJnlLine.FILTERGROUP := 2;
      ExJnlLine.SETRANGE("Journal Template Name",ExJnlTemplate.Name);
      ExJnlLine.FILTERGROUP := 0;

      ExJnlLine."Journal Template Name" := '';
      ExJnlLine."Journal Batch Name" := ExJnlBatch.Name;
      PAGE.RUN(ExJnlTemplate."Page ID",ExJnlLine);
    END;

    PROCEDURE OpenJnl@2(VAR CurrentJnlBatchName@1000 : Code[10];VAR ExJnlLine@1001 : Record 50013);
    BEGIN
      CheckTemplateName(ExJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      ExJnlLine.FILTERGROUP := 2;
      ExJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      ExJnlLine.FILTERGROUP := 0;
    END;

    PROCEDURE OpenJnlBatch@12(VAR ExJnlBatch@1000 : Record 50014);
    VAR
      ExJnlTemplate@1002 : Record 50012;
      ExJnlLine@1004 : Record 50013;
      JnlSelected@1003 : Boolean;
    BEGIN
      IF ExJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EXIT;
      ExJnlBatch.FILTERGROUP(2);
      IF ExJnlBatch.GETFILTER("Journal Template Name") <> '' THEN BEGIN
        ExJnlBatch.FILTERGROUP(0);
        EXIT;
      END;
      ExJnlBatch.FILTERGROUP(0);

      IF NOT ExJnlBatch.FIND('-') THEN BEGIN
        IF NOT ExJnlTemplate.FINDFIRST THEN
          TemplateSelection(0,FALSE,ExJnlLine,JnlSelected);
        IF ExJnlTemplate.FINDFIRST THEN
          CheckTemplateName(ExJnlTemplate.Name,ExJnlBatch.Name);
        ExJnlTemplate.SETRANGE(Recurring,TRUE);
        IF NOT ExJnlTemplate.FINDFIRST THEN
          TemplateSelection(0,TRUE,ExJnlLine,JnlSelected);
        IF ExJnlTemplate.FINDFIRST THEN
          CheckTemplateName(ExJnlTemplate.Name,ExJnlBatch.Name);
        ExJnlTemplate.SETRANGE(Recurring);
      END;
      ExJnlBatch.FIND('-');
      JnlSelected := TRUE;
      ExJnlBatch.CALCFIELDS(Recurring);
      ExJnlTemplate.SETRANGE(Recurring,ExJnlBatch.Recurring);
      IF ExJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        ExJnlTemplate.SETRANGE(Name,ExJnlBatch.GETFILTER("Journal Template Name"));
      CASE ExJnlTemplate.COUNT OF
        1:
          ExJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,ExJnlTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      ExJnlBatch.FILTERGROUP(2);
      ExJnlBatch.SETRANGE("Journal Template Name",ExJnlTemplate.Name);
      ExJnlBatch.FILTERGROUP(0);
    END;

    PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      ExJnlBatch@1002 : Record 50014;
    BEGIN
      ExJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT ExJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        IF NOT ExJnlBatch.FINDFIRST THEN BEGIN
          ExJnlBatch.INIT;
          ExJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          ExJnlBatch.SetupNewBatch;
          ExJnlBatch.Name := Text004;
          ExJnlBatch.Description := Text005;
          ExJnlBatch.INSERT(TRUE);
          COMMIT;
        END;
        CurrentJnlBatchName := ExJnlBatch.Name;
      END;
    END;

    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR ExJnlLine@1001 : Record 50013);
    VAR
      ExJnlBatch@1002 : Record 50014;
    BEGIN
      ExJnlBatch.GET(ExJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE SetName@5(CurrentJnlBatchName@1000 : Code[10];VAR ExJnlLine@1001 : Record 50013);
    BEGIN
      ExJnlLine.FILTERGROUP := 2;
      ExJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      ExJnlLine.FILTERGROUP := 0;
      IF ExJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@6(VAR CurrentJnlBatchName@1000 : Code[10];VAR ExJnlLine@1001 : Record 50013) : Boolean;
    VAR
      ExJnlBatch@1002 : Record 50014;
    BEGIN
      COMMIT;
      ExJnlBatch."Journal Template Name" := ExJnlLine.GETRANGEMAX("Journal Template Name");
      ExJnlBatch.Name := ExJnlLine.GETRANGEMAX("Journal Batch Name");
      ExJnlBatch.FILTERGROUP(2);
      ExJnlBatch.SETRANGE("Journal Template Name",ExJnlBatch."Journal Template Name");
      ExJnlBatch.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,ExJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := ExJnlBatch.Name;
        SetName(CurrentJnlBatchName,ExJnlLine);
      END;
    END;

    PROCEDURE GetEx@7(ExNo@1000 : Code[20];VAR ExName@1001 : Text[50]);
    VAR
      ExamplePerson@1002 : Record 50010;
    BEGIN
      IF ExNo <> OldExNo THEN BEGIN
        ExName := '';
        IF ExNo <> '' THEN
          IF ExamplePerson.GET(ExNo) THEN
            ExName := ExamplePerson.Name;
        OldExNo := ExNo;
      END;
    END;

    BEGIN
    END.
  }
}

