OBJECT Codeunit 50006 Ex. Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=12-03-14;
    Time=19:58:06;
    Modified=Yes;
    Version List=Exercise 3;
  }
  PROPERTIES
  {
    TableNo=50013;
    OnRun=BEGIN
            ExJnlLine.COPY(Rec);
            Code;
            COPY(ExJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;ESM=no puede contener un filtro cuando se registra un diario peri¢dico;FRC=ne peut pas ˆtre filtr‚ lors du report des journaux r‚currents;ENC=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the posting report?;ESM=¨Confirma que desea registrar las l¡ns. de diario e imprimir el informe de reg.?;FRC=Voulez-vous reporter les lignes du journal et imprimer le rapport d''affichage?;ENC=Do you want to post the journal lines and print the posting report?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;ESM=No hay nada que registrar.;FRC=Il n''y a rien … reporter.;ENC=There is nothing to post.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;ESM=Se han registrado correctamente las l¡neas del diario.;FRC=Les lignes de journal ont ‚t‚ report‚es avec succŠs.;ENC=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'ENU="The journal lines were successfully posted. ";ESM="Se han registrado correctamente las l¡neas del diario. ";FRC="Les lignes de journal ont ‚t‚ report‚es avec succŠs. ";ENC="The journal lines were successfully posted. "';
      Text005@1005 : TextConst 'ENU=You are now in the %1 journal.;ESM=Se encuentra en el diario %1.;FRC=Vous ˆtes maintenant dans le journal %1.;ENC=You are now in the %1 journal.';
      ExJnlTemplate@1006 : Record 50012;
      ExJnlLine@1007 : Record 50013;
      ExReg@1008 : Record 50015;
      ExJnlPostBatch@1009 : Codeunit 50004;
      TempJnlBatchName@1010 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH ExJnlLine DO BEGIN
        ExJnlTemplate.GET("Journal Template Name");
        ExJnlTemplate.TESTFIELD("Posting Report ID");
        IF ExJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT CONFIRM(Text001) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        ExJnlPostBatch.RUN(ExJnlLine);

        IF ExReg.GET("Line No.") THEN BEGIN
          ExReg.SETRECFILTER;
          REPORT.RUN(ExJnlTemplate."Posting Report ID",FALSE,FALSE,ExReg);
        END;

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004 +
              Text005,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

